switchtolayout;#make sure it is layoutmode
closeall;
deleteall;
clear;

groupscope("::model");#go to the main scope
um = 10^-6;

filename_RI = "index.txt";
filename_FIELD = "field.mat";
filename_save = "result.mat";
filename_fsp= "temp.fsp";
filename_optical= "optical.mat";

matlabload(filename_optical);

save(filename_fsp);

##in the mat file are the following variables
?base_index;
?lambda;
?size(RI);
?resolution;
?roi;
?return_trans;
?return_ref;
?return_vol;
?is_plane_wave;
?phi;
?theta;
?para_pol;
?ortho_pol;
?shutoff_min;
?dt_stability_factor;

dx = resolution(1);
dy = resolution(2);
dz = resolution(3);
# import the data from file
addimport;
set("name","refractive_index");
x = (1:size(RI,1))*dx;
y = (1:size(RI,2))*dy;
z = (1:size(RI,3))*dz;
importnk2(RI,x,y,z);

x_num=get("data x points");
y_num=get("data y points");
z_num=get("data z points");

if(pml_x){
    min_x=get("x min");
    max_x=get("x max");
}else{
    min_x=get("x min");
    max_x=(get("x max")+dx)/3;
    min_x=min_x+max_x;
    max_x=2*max_x-dx;
    x_num=x_num/3;
}
if(pml_y){
    min_y=get("y min");
    max_y=get("y max");
}else{
    min_y=get("y min");
    max_y=(get("y max")+dy)/3;
    min_y=min_y+max_y;
    max_y=2*max_y-dy;
    y_num=y_num/3;
}
if(pml_z){
    min_z=get("z min");
    max_z=get("z max");
}else{
    min_z=get("z min");
    max_z=(get("z max")+dz)/3;
    min_z=min_z+max_z;
    max_z=2*max_z-dx;
    z_num=z_num/3;
}

print('min/max x = '+num2str(min_x)+'  /  '+num2str(max_x));
print('min/max y = '+num2str(min_y)+'  /  '+num2str(max_y));
print('min/max z = '+num2str(min_z)+'  /  '+num2str(max_z));

addfdtd;
#size of the volume
set("dimension",2);  #  1 = 2D, 2 = 3D
set("x min",min_x);
set("x max",max_x);
set("y min",min_y);
set("y max",max_y);
set("z min",min_z);
set("z max",max_z);
#boundary
if(pml_x){
    set('x min bc','PML');
    set('x max bc','PML');
    set('pml profile',3);
}else{
    set('x min bc','Bloch');
}
if(pml_y){
    set('y min bc','PML');
    set('y max bc','PML');
    set('pml profile',3);
}else{
    set('y min bc','Bloch');
}
if(pml_z){
    set('z min bc','PML');
    set('z max bc','PML');
    set('pml profile',3);
}else{
    set('z min bc','Bloch');
}
#base index
set('index',base_index);
#mesh size
set('min mesh step',0);

set('mesh type',3);

set('define x mesh by',4);
set('mesh cells x',x_num-1);

set('define y mesh by',4);
set('mesh cells y',y_num-1);

set('define z mesh by',4);
set('mesh cells z',z_num-1);
set("auto shutoff min", shutoff_min);
set("dt stability factor", dt_stability_factor);
#mesh_x=getresult("FDTD","x"); #get mesh location

if(is_plane_wave)
{
    addplane;
    set("name","pol1");
    set("injection axis","z");
    set("angle theta",theta/pi*180);
    set("angle phi",phi/pi*180);
    set("amplitude",abs(para_pol));
    set("phase",angle(para_pol)/pi*180);


    set("x min",min_x);
    set("x max",max_x);
    set("y min",min_y);
    set("y max",max_y);
    set("polarization angle",0);
    
    set("z",min_z);
    
    set("center wavelength",lambda*um);
    set("wavelength span",0);
    
    addplane;
    set("name","pol2");
    set("injection axis","z");
    set("angle theta",theta/pi*180);
    set("angle phi",phi/pi*180);
    set("amplitude",abs(ortho_pol));
    set("phase",angle(ortho_pol)/pi*180);

    set("x min",min_x);
    set("x max",max_x);
    set("y min",min_y);
    set("y max",max_y);
    set("polarization angle",90);
       
    
    set("z",min_z);
    
    set("center wavelength",lambda*um);
    set("wavelength span",0);
}else{
    addimportedsource;
    
    importdataset(filename_FIELD);
    #set("injection axis",3);
    
    set("x",0);
    set("y",0);
    
    set("x",-get("x min"));
    set("y",-get("y min"));
    
    set("z",min_z);
    
    set("center wavelength",lambda*um);
    set("wavelength span",0);
}

if(pml_x){
    obs_min_x = min_x+roi(1)-2*dx;
    obs_max_x = min_x+roi(2)-2*dx;
}else{
    obs_min_x = min_x;
    obs_max_x = max_x;
}
if(pml_y){
    obs_min_y = min_y+roi(3)-2*dy;
    obs_max_y = min_y+roi(4)-2*dy;
}else{
    obs_min_y = min_y;
    obs_max_y = max_y;
}
if(pml_z){
    obs_min_z = min_z+roi(5)-2*dz;
    obs_max_z = min_z+roi(6)-2*dz;
}else{
    obs_min_z = min_z;
    obs_max_z = max_z;
}

if(return_trans){
addpower;
set("name","field_profile_trans");
set("monitor type",7);  # 2D z-normal

set("x min",obs_min_x);
set("x max",obs_max_x);
set("y min",obs_min_y);
set("y max",obs_max_y);

set("z",max_z);

}
if(return_ref){
addpower;
set("name","field_profile_ref");
set("monitor type",7);  # 2D z-normal

set("x min",obs_min_x);
set("x max",obs_max_x);
set("y min",obs_min_y);
set("y max",obs_max_y);

set("z",min_z);

}

if(return_vol){
addpower;
set("name","field_profile_vol");
set("monitor type",8);  # 2D z-normal

set("x min",obs_min_x);
set("x max",obs_max_x);
set("y min",obs_min_y);
set("y max",obs_max_y);
set("z min",obs_min_z);
set("z max",obs_max_z);
}




#run the simulation
t_start = now;
run;
t_code = now - t_start;
? 'Total elapsed time: ' + num2str(t_code/1000) + 's';
#collect result
res_vol = 0;
res_trans = 0;
res_ref = 0;

if(return_vol){res_vol = getresult("field_profile_vol", "E");}
if(return_trans){res_trans = getresult("field_profile_trans", "E");}
if(return_ref){res_ref = getresult("field_profile_ref", "E");}


matlabsave(filename_save,res_vol,res_trans,res_ref);


